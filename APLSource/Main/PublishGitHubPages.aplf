 PublishGitHubPages←{
     ⍝ ⍺ ←→ [Project Folder|Space]
     ⍝ ⍵ ←→ Message
     ⍺←0
     D←⎕SE.zApps.Dado.Dado.Main
     e←D.IsDirty 0
     e:'Uncommitted changes, not published.'
     s←D.GetProjectSpace ⍺
     sf tf←GetSourceAndTarget s
     n←'/',s.AcreConfig._Name
     _←n Build sf tf s
     p←s.AcreConfig.ProjectFolder
     c←p D.GetCurrentBranch''
     _←p InitGitHubPages''                      ⍝ Delete all files too?
     'gh-pages'≢p D.GetCurrentBranch'':'Unable to checkout gh-pages branch'
     op←('Wildcard' 1)('IfExists' 'Replace')
     _←p(⎕NCOPY⍠op)tf,'/*'
     git←p∘D.RunGitCommand
     _←git'add --all'
     _←git'commit -m "Deployed with Exigo"'
     _←git'push origin gh-pages --force'
     _←p D.Checkout c
     0
 }
